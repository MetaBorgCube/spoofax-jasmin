module editor/builders

imports
	stratego/metaborg
	analysis/names/project	
	syntax/ASM
	trans/translator/-
	
rules
	
	generate-class:
		(selected, position, ast, path, project-path) -> None()
		where
			filename := <guarantee-extension(|"class")> path
		where
			<write-class-file(|filename)> <jasmin-file-to-asm-class> ast;
			<refresh-workspace-file> filename;
			result := None()
			
	translate-class:
		(selected, position, ast, path, project-path) -> (filename, result)
		with
			filename := <guarantee-extension(|"asm.aterm")> path
		where
			result := <jasmin-file-to-asm-class> ast
			
	execute-class:
		(selected, position, ast, path, project-path) -> (filename, result)
		with
			filename := <guarantee-extension(|"result.txt")> path
		with
			cname := <class-name ; string-replace(|"/",".")> ast;
			cpath := <concat-strings; prim("local_path")> [project-path, "/", <dirname> path];
			<execute-java(|cpath); process-output> cname => result
	
	generate-and-execute-class:
		(selected, position, ast, path, project-path) -> (filename, result)
		with
			class-filename := <guarantee-extension(|"class")> path;
			filename := <guarantee-extension(|"result.txt")> path
		with
			<write-class-file(|class-filename)> <jasmin-file-to-asm-class> ast;
			<refresh-workspace-file> class-filename;
			cname := <class-name ; string-replace(|"/",".")> ast;
			cpath := <concat-strings; prim("local_path")> [project-path, "/", <dirname> path];
			<execute-java(|cpath); process-output> cname => result
			
				
		
	
	process-output: Output(result, "") -> result

	process-output: Output(result, error) -> $[[result] Runtime error: [error]]
		where not (<""> error)
		
signature constructors
	
	Output: String * String -> Term

strategies
	
	external execute-java(|path)
	external write-class-file(|fname)
