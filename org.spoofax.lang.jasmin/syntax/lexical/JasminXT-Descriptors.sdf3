module lexical/JasminXT-Descriptors

imports lexical/JasminXT-Names

context-free start-symbols
  FieldDescriptor
  ReturnDescriptor
  BaseType

context-free syntax
  FieldDescriptor.JBCFieldDesc = [[FieldType]]

  ParameterDescriptor   = [[FieldType]]
  AnnotationDescriptor  = [[FieldType]] // NB: This is a guess
  ReturnDescriptor      = [[FieldType]]
  ReturnDescriptor.Void = [V]

  FieldType = [[BaseType]]
  FieldType = [[ObjectType]]
  FieldType = [[ArrayType]]

  BaseType.Byte    = [B]
  BaseType.Char    = [C]
  BaseType.Double  = [D]
  BaseType.Float   = [F]
  BaseType.Int     = [I]
  BaseType.Long    = [J]
  BaseType.Short   = [S]
  BaseType.Boolean = [Z]

  ComponentType = [[FieldType]]

syntax // avoid whitespaces in method descriptors, reference types, array types	
  MethodDescriptor-CF.JBCMethodDesc = "(" ParameterDescriptor-CF* ")" ReturnDescriptor-CF

  ObjectType.Reference = "L" ClassRef-CF ";"	
  ArrayType-CF.Array = "[" ComponentType-CF
